module Stack2nix.External.Cabal2nix (
  runCabal2nix
  ) where

import           Data.List      (stripPrefix, takeWhile)
import           Data.Monoid    ((<>))
import           Data.Text      (Text, unpack)
import           System.Exit    (ExitCode (..))
import           System.Process (proc, readCreateProcessWithExitCode)

-- Requires cabal2nix >= 2.2 in PATH
runCabal2nix :: FilePath -> Maybe Text -> Maybe FilePath -> IO ()
runCabal2nix dir commit subpath = do
  (exitCode, stdout, _stderr) <- readCreateProcessWithExitCode (proc "cabal2nix" args) ""
  case exitCode of
    ExitSuccess -> writeFile (pname stdout <> ".nix") stdout
    _           -> error $ "cabal2nix failed on directory: " <> dir
  where
    args :: [String]
    args = concat
      [ maybe [] (\c -> ["--revision", unpack c]) commit
      , maybe [] (\d -> ["--subpath", d]) subpath
      , [dir]
      ]

    pname :: String -> String
    pname = pname' . lines

    pname' :: [String] -> String
    pname' [] = error "nix expression generated by cabal2nix is missing the 'pname' attr"
    pname' (x:xs) =
      case stripPrefix "  pname = \"" x of
        Just x' -> takeWhile (/= '"') x'
        Nothing -> pname' xs
