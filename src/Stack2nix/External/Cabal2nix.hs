module Stack2nix.External.Cabal2nix (
  cabal2nix
  ) where

import           Cabal2nix                   (cabal2nix')
import           Data.List                   (stripPrefix, takeWhile)
import           Data.Maybe                  (fromMaybe)
import           Data.Monoid                 ((<>))
import           Data.Text                   (Text, unpack)
import           Data.Time                   (UTCTime, defaultTimeLocale,
                                              formatTime)
import           Language.Nix.PrettyPrinting (pPrint)
import           System.FilePath             ((</>))
import           System.IO                   (hPutStrLn, stderr)

cabal2nix :: FilePath -> Maybe Text -> Maybe FilePath -> Maybe FilePath -> Maybe UTCTime -> IO ()
cabal2nix uri commit subpath outDir hSnapshot = do
  let runCmdArgs = args $ fromMaybe "." subpath
  hPutStrLn stderr $ unwords ("+ cabal2nix":runCmdArgs)
  result <- cabal2nix' runCmdArgs
  case result of
    Right deriv ->
      let out = show $ pPrint deriv
          basename = pname out <> ".nix"
          fname = maybe basename (</> basename) outDir
      in
      writeFile fname out
    Left err ->
      hPutStrLn stderr $ unwords [ "ERROR: cabal2nix failed on"
                                 , uri
                                 , "(rev: " ++ show commit ++ ";"
                                 , "subpath: " ++ show subpath ++ "):"
                                 , "\n" ++ show err
                                 ]
  where
    args :: FilePath -> [String]
    args dir = concat
      [ maybe [] (\c -> ["--revision", unpack c]) commit
      , ["--subpath", dir]
      , ["--system", "x86_64-linux"]
      , ["--no-check", "--no-haddock"]
      , maybe [] (\snapshot -> ["--hackage-snapshot", iso8601 snapshot]) hSnapshot
      , [uri]
      ]

    iso8601 :: UTCTime -> String
    iso8601 = formatTime defaultTimeLocale "%FT%T%QZ"

    pname :: String -> String
    pname = pname' . lines

    pname' :: [String] -> String
    pname' [] = error "nix expression generated by cabal2nix is missing the 'pname' attr"
    pname' (x:xs) =
      case stripPrefix "  pname = \"" x of
        Just x' -> takeWhile (/= '"') x'
        Nothing -> pname' xs
